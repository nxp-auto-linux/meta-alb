# We have a conf and classes directory, add to BBPATH
# Note how we PREpend BBPATH. This is due to the fact that
# bitbake does not override classes per layer as it does
# recipes. Classes are processed strictly in BBPATH order.
# As we unfortunately have to override standard classes for
# bug fixing, we prepend instead of append our path.
BBPATH =. "${LAYERDIR}:"

FSL_EULA_FILE = "${LAYERDIR}/EULA"

LICENSE_PATH += "${LAYERDIR}/custom-licenses"

BBFILE_COLLECTIONS += "alb"
BBFILE_PATTERN_alb := "^${LAYERDIR}"
BBFILE_PRIORITY_alb = "8"

# The dynamic-layers directory hosts the extensions and layer specific
# modifications related to NXP products.
#
# The .bbappend and .bb files are included if the respective layer
# collection is available.
BBFILES += "${@' '.join('${LAYERDIR}/dynamic-layers/%s/recipes*/*/*.bbappend' % layer \
		for layer in BBFILE_COLLECTIONS.split())}"
BBFILES += "${@' '.join('${LAYERDIR}/dynamic-layers/%s/recipes*/*/*.bb' % layer \
		for layer in BBFILE_COLLECTIONS.split())}"

# We want our meta-alb recipes to be applied *after* the
# dynamic layer changes. This is for us to use the dynamic-layers to
# override existing layers before we apply our changes
BBFILES += " \
        ${LAYERDIR}/recipes-*/*/*.bb \
        ${LAYERDIR}/recipes-*/*/*.bbappend \
        ${LAYERDIR}/classes/*.bb* \
"

# Set LAYERSERIES_COMPAT variable for meta-alb layer
# to match the version "zeus" of OpenEmbedded-Core
LAYERSERIES_COMPAT_alb = "zeus dunfell gatesgarth"
