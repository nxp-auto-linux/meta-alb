#@TYPE: Machine
#@NAME: NXP LX2160ABLUEBOX3
#@SOC: LSCH3
#@DESCRIPTION: Machine configuration for running LX2160ABLUEBOX3 in 64-bit mode
#@MAINTAINER: Heinz Wrobel <heinz.wrobel@nxp.com>

require conf/machine/include/qoriq-atf.inc
require conf/machine/include/qoriq-common.inc
require conf/machine/include/qoriq-arm64.inc
require conf/machine/include/arm/arch-arm64.inc

MACHINEOVERRIDES =. "fsl-lsch3:lx2160a:"
# required for sdcard deploy
HOSTTOOLS += "parted mkfs.vfat mcopy"

IMAGE_FSTYPES += "ext4"
EXTRA_IMAGECMD:ext4 = "-i 4096 -L ${IMAGE_NAME}"

# We also want to permit SD card image generation.
# This sets up the defaults, just in case the image
# specifies an addition of "sdcard" to IMAGE_FSTYPES.
# Note that we can not boot U-Boot from SD Card on BB Classic
SDCARD_ROOTFS ?= "ext4"
UBOOT_SUFFIX_SDCARD ?= "${UBOOT_SUFFIX}"

MACHINE_FEATURES += "optee"

#KERNEL_CLASSES  = " kernel-fitimage "
#KERNEL_IMAGETYPES = "fitImage"

DTB_LOAD = "0x90000000"
UBOOT_ENTRYPOINT = "0x80200000"

UBOOT_CONFIG ??= "tfa-secure-boot tfa"
UBOOT_CONFIG[tfa] = "${MACHINE}_tfa_defconfig,,u-boot-dtb.bin"
UBOOT_CONFIG[tfa-secure-boot] = "${MACHINE}_tfa_SECURE_BOOT_defconfig,,u-boot-dtb.bin"

UBOOT_ENV_INC_NAME:append:fsl-lsch3 = " distro_boot"

KERNEL_DEVICETREE ?= "freescale/fsl-lx2160a-bluebox3.dtb"
KERNEL_DEFCONFIG ?= "defconfig"

RCWXSPI ?= "PPUUPPUU_PP_RR_31_2_2/rcw_2200_750_3200_xspi_31_2_2"
RCWSD ?= "PPUUPPUU_PP_RR_31_2_2/rcw_2200_750_3200_sd_31_2_2"
RCWEMMC ?= "PPUUPPUU_PP_RR_31_2_2/rcw_2200_750_3200_sd_31_2_2"

EXTRA_IMAGEDEPENDS += "management-complex mc-utils rcw ddr-phy ${ATF}"
USE_VT = "0"

# Given modern kernel sizes, we do not use virtual/kernel but a
# size reduced version suitable for a recovery Linux in flash.
require include/lxflashmap.inc
FLASHIMAGE_KERNEL = "linux-flash"
FLASHIMAGE_KERNEL_BASENAME = "kernel-flash"
# QSPI bank 4 support is done in HW. There is no need to duplicate
# the image within a single flash.
FLASHIMAGE_SIZE = "64"
FLASHIMAGE_BANK4 = "no"
FLASHIMAGE_RESET = "${ATF}"
FLASHIMAGE_RESET_FILE = "${ATF_RESET_FILE_XSPI}"
FLASHIMAGE_EXTRA9 ?= "linux-kernelitb"
FLASHIMAGE_EXTRA9_FILE ?= "kernel-flash-withrootfs.itb"
FLASHIMAGE_EXTRA9_OFFSET ?= "${FLASHIMAGE_KERNEL_OFFSET}"
FLASHIMAGE_EXTRA1 = "management-complex"
FLASHIMAGE_EXTRA1_FILE = "mc_app/mc.itb"
FLASHIMAGE_EXTRA1_OFFSET = "${FLASHIMAGE_DPAA2_MC_OFFSET}"
FLASHIMAGE_EXTRA2 = "mc-utils"
FLASHIMAGE_EXTRA2_FILE = "mc-utils/custom/${MACHINE}-dpl-ethbluebox.31.dtb"
FLASHIMAGE_EXTRA2_OFFSET = "${FLASHIMAGE_DPAA2_DPL_OFFSET}"
FLASHIMAGE_EXTRA3 = "mc-utils"
FLASHIMAGE_EXTRA3_FILE = "mc-utils/custom/${MACHINE}-dpc-31.dtb"
FLASHIMAGE_EXTRA3_OFFSET = "${FLASHIMAGE_DPAA2_DPC_OFFSET}"
FLASHIMAGE_EXTRA4 = "u-boot-environment"
FLASHIMAGE_EXTRA4_FILE = "u-boot-flashenv-${MACHINE}.bin"
FLASHIMAGE_EXTRA4_OFFSET = "${FLASHIMAGE_UBOOT_ENV_OFFSET}"
FLASHIMAGE_EXTRA5 = "ddr-phy"
FLASHIMAGE_EXTRA5_FILE = "ddr-phy/fip_ddr_all.bin"
FLASHIMAGE_EXTRA5_OFFSET = "${FLASHIMAGE_DDR_PHY_OFFSET}"

FLASHIMAGE_UBOOT = "${ATF}"
FLASHIMAGE_UBOOT_FILE = "${ATF_IMAGE_FILE_XSPI}"

# Generating an SDHC image to be directly booted with RCW=0x40
SDCARD_RCW = "${ATF}"
SDCARD_RCW_NAME = "${ATF_RESET_FILE_SD}"

SDCARD_BINARY_SPACE = "65536"
UBOOT_NAME_SDCARD = "${ATF_IMAGE_FILE_SD}"
SDCARDIMAGE_EXTRA4_FILE = "u-boot-flashenv-sd-${MACHINE}.bin"
UBOOT_BOOTSPACE_OFFSET  = "${FLASHIMAGE_UBOOT_OFFSET}"
BOOT_SPACE ?= "65536"
# Currently we only use the SDCard environment.
# For Flash/factory images we may want to use 'u-boot-flashenv' or
# one derived from it.
UBOOT_ENV_NAME:append = " u-boot-flashenv-sd"

# No emergency Ubuntu Linux on the SD card as flash like element,
# so we disable inheritance from above by explicitly clearing it.
# We also clear it for boot flash.
FLASHIMAGE_EXTRA9_FILE:ubuntu = ""
SDCARDIMAGE_EXTRA9_FILE:ubuntu = ""

IMAGE_FSTYPES += "sdcard"
